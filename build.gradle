plugins {
    id "net.ltgt.apt" version "0.10"
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

group 'comp354'
version '1.0-SNAPSHOT'

mainClassName = 'com.github.comp354project.Main'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// In this section you declare where to find the dependencies of your project
repositories {
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // Project dependencies
    compile group: 'org.wildfly.swarm', name: 'javafx', version: '2018.1.0'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.21.0.1'
    compile 'com.google.dagger:dagger:2.14.1'
    compile 'org.projectlombok:lombok:1.16.20'
    compile 'commons-dbutils:commons-dbutils:1.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.10.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.10.0'
    compile group: 'com.google.guava', name: 'guava', version: '15.0'
    apt "org.projectlombok:lombok:1.16.20"
    apt 'com.google.dagger:dagger-compiler:2.14.1'
    compile 'com.j256.ormlite:ormlite-jdbc:4.48'
    compile group: 'com.jfoenix', name: 'jfoenix', version: '1.5.0'
    compile group: 'javax.mail', name: 'mail', version: '1.4.7'
    // Test dependencies
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.mockito:mockito-core:1.+"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1' //version required
}

test {
    environment "db", "sqlite/test/mymoney.db"
    classpath += files("config/test/log")
}

run {
    environment "db", "sqlite/run/mymoney.db"
    classpath += files('config/run/log')
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.github.comp354project.Main'
    }
    archiveName = "MyMoney.jar"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task zip(type: Zip) {
    delete fileTree('.') {
        include '*.zip'
    }
    from ('build/libs') {
        include '*.jar'
    }
    from ('sqlite'){
        include 'mymoney_backup.db'
        rename { 'mymoney.db' }
    }
    archiveName 'MyMoney.zip'
    destinationDir(file('.'))
}

tasks.zip.dependsOn(fatJar)